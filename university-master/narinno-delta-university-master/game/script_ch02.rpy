label chapter_2:

    # Chapter 2: Mount Resshan
    # POV 1: Kim
    # [Background: view of a wild landscape, from a vantage point.]

    scene bg hiking with fade

    "If the neighborhood surrounding the NDU campus was impressive, then the temperate 
    rainforest that made up Resshan National Park was breathtaking."

    "Kim had vaguely known what went into terraforming the colonies, but seeing the results 
    in person was something else."

    "The native foliage and microfauna of Narinno Delta intertwined effortlessly with 
    assorted, carefully limited specimens from the Associated homeworlds."

    "Kim could only imagine how rigorously these nonnative introductions had to have been 
    modeled to ensure that the local life would continue to thrive."

    "She thought she spied the occasional Terran wildflower amidst the green sprawl, but 
    they blended in beautifully, one paint stroke in a thousand."

    "How far behind the pack she’d fallen by now to ogle the view was a mystery. Thankfully, 
    she had an excuse."

    # [SFX: footsteps. Sprite: an exhausted Nakoa trudges into the frame.]
    # TODO: neutral/thoughtful sprites for Nakoa, I'm using "stern" too much...

    show nakoa worried_talk at center_1 with dissolve

    nk "Haah… haah…"

    nk "Please, you don’t have to keep stopping for me…"

    ki "I promised I’d walk with you, didn’t I? Shorties gotta stick together."

    ki "I don’t mind. Really."

    nk "If you’re sure."

    nk happy @ happy_talk "Oof… guess it’s my own fault…! Punishment for holing up in the mancave."

    ki "Mancave? I’m curious what an onka mancave even looks like."

    "The thought slipped out unfiltered, and she immediately regretted it."

    ki "I-I mean, any mancave, I didn’t mean—"

    nk happy_talk "Heh. Nah, I get it."

    nk friendly "It’s not really a concept back home. Fair for you to be curious."

    nk stern "We’ve got a word that’s…similar? {i}Qoldaran{/i}. Literally ‘mother den.’"

    nk friendly "But you could probably roughly translate it to ‘womancave’ and get the gist, ha."

    nk stern "Naturally, I refuse to apply it to myself."

    ki "Naturally."

    ki "…Actually wait, no, hang on."

    show nakoa worried with dissolve_f

    ki "You could use it, right? Screw gender norms, this is the galactic age. Why can’t 
    a guy have a womancave?"

    nk angry @ angry_talk "I don’t want to use it. End of sentence."

    ki "Oh… okay. I’m sorry, I didn’t mean to offend—"

    nk scowl "You didn’t. It’s a me thing."

    nk stern "...Like, I should be able to be a man however I want, right? I shouldn’t have 
    to use woman-y terms to describe my… me-ness, y’know?"

    ki "I… yes? I think?"

    "She had to admit, she had not expected this chat with a still-relatively-new friend to 
    pivot so hard into gender theory."
    
    "Nor was she as knowledgeable about the onka-specific version 
    of gender theory as she’d have liked."

    show nakoa happy with dissolve_f

    ki "Like, yeah. You define yourself how you want, and that’s that."

    ki "It’s not something I ever thought too much about, I admit. I’m a girl, I read as 
    more girly than not, but I’m just me in the end, right?"

    ki "I’ve heard it was a much bigger deal on Earth pre-contact. We studied it in my 
    history classes: the suffragettes, the different waves of feminism, pride, the gender 
    revolution."

    show nakoa worried with dissolve_f

    ki "It sounds like all that might be a big deal on Tandoro, still?"

    nk worried_talk "Yeah. I’ve always been me, too, but back home it felt all… weird. Like I wasn’t 
    supposed to be me."

    nk scowl "Mom never liked the clothes I wanted to wear. The stuff I wanted to do."

    # [Sprite: exasperated.]
    nk stern "But I’ve got {i}you{/i}, right? Terrans! Terran dudes, being like… me!"

    nk worried @ worried_talk "I needed it. Y’know? To not feel like a freak."

    ki "So that’s why you came to a colony? To be around—"

    nk worried_talk "—guys like me, yeah. That was part of it, at least."

    nk grumpy talk "Mostly just wanted to get out of that house. It got worse after Tani left."

    # [Sprite: grumbly.]
    nk scowl "Favorite child…"

    nk worried_talk "Not that that’s on her. I love my sis. And she knew how to play her hand."

    nk stern "And she {i}got{/i} me, y’know? We both—"

    "Nakoa cut himself off."
    
    # TODO: better, more strained sprite here
    show nakoa smug with dissolve_f

    "Then he closed his eyes and took a measured, extremely deliberate inhale."

    nk friendly "I’m shutting up now! You, go. Your turn to trauma-dump."

    ki "What?"

    nk stern "I poured my darkest secrets out on a relative stranger without permission. Best 
    I can make it up to you is lettin’ you do the same thing back."

    ki "Well, um."

    ki "I… don’t really know what to say about myself… to a relative stranger."

    # TODO: sad/regretful sprite
    nk worried_talk "...Yeah, fair. Totally fair. ’M sorry."

    nk stern "Well, if you think of a way I can make it up to you, lemme know. I owe you one."

    "He was offering his hand for a Terran handshake. It was a more sincere gesture than Kim 
    expected."

    "But she liked Nakoa’s sincerity. It was refreshing."

    ki "Heh. Deal, then."

    show nakoa happy with dissolve_f

    ki "So, you hanging in there? Had a chance to catch your breath?"

    nk worried_talk "...Aw, man, we weren’t even walking this whole time?"

    nk happy @ happy_talk "No wonder talking felt so easy!"

    "That made her laugh. The amusement seemed to be shared."

    "When next they began to walk again, Kim made sure to move slowly."

    ki "So, you’re not a fan of hiking, huh?"

    nk stern "Meh, it’s not intrinsically bad. I just haven’t enjoyed it in recent years."

    nk worried @ worried_talk "I got more sedentary after middle school. Lost that endless kid energy, y’know."

    ki "Ah, yeah. Totally understandable."

    nk happy_talk "Tani and I were little gremlins as kids, apparently. Parents keep telling 
    stories about us running all over the place, and I believe ’em."

    nk worried_talk "I vaguely remember Tani slowing down as she got older, and… I guess I felt bad 
    leaving her behind? So I slowed down too. Got into different hobbies."

    nk friendly "I might not be exercising by body as much as I should, but at least I’m 
    exercising my brain, yeah?"

    ki "Oh? What is it you do, mostly?"

    nk happy @ happy_talk "Console restoration! Refurbishment, rewiring for modern power sources and I/O 
    components, y’know."

    ki "Oh! You mean, like, antique game consoles and computers?"

    nk happy_talk "Exactly! And it’s not just wires and soldering. Had to teach myself a lot about 
    encoding and signal conversion."

    nk friendly "Y’know, ’cause two devices don’t just magically work when you plug them 
    together. They have to know how to talk to each other."

    ki "Right, that makes sense."

    ki "Is it kind of like that one bit from {i}The Ambassador{/i}? Have you seen that movie?"

    nk happy @ happy_talk "Ooooh, yeah, the first contact docudrama? With Nekhalle Turren playing the—"

    ki "Yeah, that one! Remember in the prologue, where the Chorran scientists had to 
    fiddle with their equipment juuust right before they realized they were getting a 
    Terran signal?"

    nk friendly "Right right right! The EM TV signal."

    ki "How historically accurate do you think that scene was?"

    nk stern "Oh, I’m sure they made up all the dialogue in that movie, but I’d have to do 
    more research on the instruments. Seemed more or less legit."

    ki "Neat. That’s a really cool hobby! I bet you know a lot of stuff I couldn’t even 
    imagine."

    nk happy @ happy_talk "Well, there’s always room for more knowledge. I want to go into computer 
    engineering while I’m here. Learn things properly, get into the industry."

    ki "That’s awesome! Sounds like a lot of fun."

    nk @ happy_talk "How about you? What do you do?"

    ki "Oh, uh."

    ki "Well, to be perfectly frank, I don’t really know."

    nk stern "...What do you mean, you ‘don’t really know’ what you do?"

    ki "I mean what I’m going to major in. I’m solid academically in most subjects, but 
    nothing’s ever really been a passion."

    ki "Feels like even my hobbies kinda rotate in and out. I tend to get really sucked 
    into a new thing—like some specific, obscure craft or online project—and love it for a 
    month, then forget about it again."

    nk worried_talk "Oof, hyperfixations will do that to ya. My sympathies."

    nk happy @ happy_talk "Any that’ve grabbed ya more than once? That you come back to?"

    ki "Hmm, good question. Um, let’s see…"

    "As she racked her brains, however, a distinct motion in the corner of her eye caught 
    her attention."

    "Up ahead at the next crest of the trail, a lone, slim figure waved her hand high to 
    hail the pair."

    # [Sprite: Karalún appears.]

    show nakoa happy at leftish_2 with ease
    show kara neutral at rightish_2 with fadeinright

    ki "Hey!"

    ka @ neutral talk "Hello, is all well?"

    ki "Ah, sorry, are we going too slow?"

    ka worried @ worried talk "No, it’s fine! Noah asked that I check on you."

    nk @ happy_talk "Eh, we’re plugging along, don’t worry."

    ki "We can pick up the pace a bit, if we need to."

    # [Sprite: Karalún grins.]

    ka grin "No, all’s well! Don’t worry."

    "Kim flinched unbidden at that mouthful of Jorgoan teeth. That was a passive-aggressive 
    smile, she was sure of it."

    "She didn’t want to keep the others up ahead waiting."

    "But nor did she want to push Nakoa’s limits..."

    # [Minigame: the three of them walk up the path. Nakoa and Karalún walk at the same speed, 
    # w/ Nakoa in the back and Kara in the front. Kim can choose to walk quickly, slowly, or stop.]

    # NOTE: I've realized that this segment sounds dangerously like an escort quest; until I can 
    # think of an elegant way to code in 2 different walk speeds, this choice should be handled 
    # with manual choices so the player doesn't have to "stop and start" in the minigame. To that 
    # end, I'll add some temp dialogue below.

menu chapter_1_walk_choice:
    "What to do?"

    "Pick up the pace.":
        $ kim_walk_pick = "Karalún"
        $ renpy.fix_rollback()

        # Kim gets too far ahead of Nakoa:

        "No, best not to risk angering the Jorgoan—{w=0.5}er, the {i}others{/i}."

        show nakoa happy at left_4
        show kara happy at center_1
        with ease

        "She hustled to catch up to Karalún and push ahead."

        define nk_s = Character("Nakoa", kind=nk, what_prefix='{size=-10}“', what_suffix='”{/size}')

        nk_s worried @ worried_talk "Oh, you can’t hear… okay."

        # Kim goes ahead of Kara:

        ka worried @ talk "Oh, it’s okay, there is no need to rush!"

        ki "You sure?"

        ka happy @ grin "Please feel free to keep talking. I’ll listen."

        "Well... she still couldn't tell if that grin was a threat, but now she felt stupid. And rude."

        "She swallowed her pride and slowed back down to her previous pace."

        # Kim reenters Nakoa’s range:

        show nakoa happy at center_1
        show kara neutral at right_4
        with ease

        ki "Sorry, what was that?"

    "Take it easy.":
        $ kim_walk_pick = "Nakoa"
        $ renpy.fix_rollback()

        "She steeled herself. It took effort to resist the urge to please, but resist she did."

        show nakoa happy at center_1
        show kara neutral at right_4
        with ease

        "Threatening smile or not, she'd made a promise to her fellow shorty. They would stick 
        together."

        ki "So, where were we?"

label chapter_1_after_walk_choice:

    # (Ideal) Dialogue if she stays in range of Nakoa:

    nk @ happy_talk "Yeah, you were saying? Hobbies you’ve come back to more than once?"

    ki "Right, right! Let’s see."

    ki "If I had to pick… I’d go with singing, I think."

    nk @ happy_talk "Oh, that’s right! You’re the Karaoke Queen."

    ki "Ahaha, not just karaoke! Though I do like it. Sometimes with my friends, 
    sometimes with my mom."

    ki "But I’ve been in lots of school choirs. In and out as I could afford an 
    extracurricular, y’know."

    ki "But whenever I wasn’t in one, I kind of missed it."

    nk @ happy_talk "I feel ya. There’s gotta be a good few options for you here, right? Let’s ask 
    Noah."

    ki "Yeah, for sure!"

    show nakoa friendly at leftish_2 with ease

    nk friendly "Hey, Karalún, how about you? Got any hobbies?"

    show kara worried at rightish_2 with ease

    ka worried talk "Oh, me? Hobbies?"

    ka neutral @ talk "Well, I did… um, what do you call it? When you fight with your body, without 
    weapons."

    nk happy @ happy_talk "Oooh, martial arts?"

    ka grin "Martial arts, yes! Thank you!"

    ka excited "I enjoyed martial arts when I was a teenager. It has been many years."

    ki "Many years? How old are—"

    show kara neutral with dissolve_f

    ki "Er, sorry, that’s rude."

    ka worried talk "No, it’s fine! I am told that I’m older than the average freshman."

    ka neutral @ talk "I am… twenty-four of your years old, if I remember the math correctly."
 
    ki "Alright, that makes sense. Cool."

    nk @ happy_talk "So how long have you been with us, in the Association?"

    ki "We don’t have to keep asking her difficult questions—"

    ka grin "No, it’s fine, it’s fine!"

    ka neutral @ talk "I’ve lived in the Association for four years. I studied your history and 
    language. Now, for this year I have a college grant."

    nk @ stern "Four years ago... So the latest Avikran evacuation, back in ’32?"

    ki "Wow, your Standard’s really good for only four years! I’m impressed!"

    ka happy @ grin "Oh, thank you! I try my best!"

    ki "And how are you liking it here?"

    ka worried talk "Ah. It’s… different. It’s hard to explain."

    ka neutral @ talk "The rescue was relief, of course. A little strange, but relief."

    nk @ worried_talk "Yeah, that’s not surprising. Return to civilization. That’s gotta be 
    disorienting."

    ka @ talk "The cultures, though? Here, it is… louder. Brighter. More variety of things, 
    if I can say that." 

    # Kim lags behind:

    # nk "Hey, all good?" [he and Kara both stop walking until Kim catches up.]

    # End of minigame. NOTE: how much of the conversation was completed during the allotted 
    # time may affect dialogue down the line.

    # [Normal game UI resumes]

    show nakoa happy at leftish_3
    show kara neutral at center_1
    with ease

    show noah at rightish_3
    with fadeinright

    no "Ah, here we all are! Everything alright?"

    ki "Yes, we’re hanging in there! We didn’t mean to keep everyone waiting."

    no "Oh, it’s fine. It’s my job to keep an eye on everyone, and I don’t think any of 
    us minded a break, either."

    "From over Noah’s shoulder, the other students—Nami, Allira, and Khurrayo, if Kim 
    recalled—made assorted sounds and gestures of agreement."

    no "We’ve got about a kilometer left, and a slight climb, then we’ll get to pitch 
    our tents. Let’s do it!"

    show nakoa grumpy with dissolve_f

    "A whoop from Nami and a whining rebuttal from Nakoa hit Kim’s ears at the same time. 
    She stifled a chuckle."

label nvl_monologue_test:

    # —
    # NOTE: was planned to be an NVL section. May take experimentation

    hide nakoa
    hide kara
    hide noah
    with fadeoutleft

    nvl clear
    nvl show 
    with Dissolve(0.2)

    nvl_narrator "With the full group reconvened, talk was lively and spirits were high."
    
    # "Between Nakoa’s quips and anecdotes, Karalún’s curious insights, Noah’s occasional 
    # brotherly check-in, and the indistinct but energetic chatter from the trio up front, 
    # Kim forgot about the passage of time."
    nvl_narrator "Nakoa’s well of quips and anecdotes never ran dry, and with Karalún's curious insights, 
    Noah’s occasional brotherly check-in, and the background chatter of the trio up ahead..."

    nvl_narrator "It was the perfect recipe for Kim to forget to perceive the passage of time."

    show bg campsite_day with dissolve
    
    nvl_narrator """
    Eventually their trail reached a walk-in campsite, or rather a string of them.
    
    A few other student groups who’d had a staggered start ahead of their own could be 
    seen in the distance, if you squinted and knew where to look between the trees.

    Noah led the team to one of the vacant plots, marked by a swath of flat earth, picnic 
    tables, and a fire pit.

    With a triumphant unfurling and planting of their “Team Cobalt” banner at the edge of 
    the clearing to mark their territory, the afternoon began in earnest.
    """

    # —
    # TODO: art here??

    nvl clear

    nvl_narrator """
    First was a late picnic lunch of deli wraps, courtesy of Nami’s pack.

    Then came the pitching of the tents, which had generously been carried by Khurrayo and 
    Allira. (In retrospect, the two quadrupeds were the obvious choices.)

    Then they played a word game featuring everyone’s native languages.

    On the hour came a more serious interlude, as Noah passed orientation handbooks around 
    the circle.

    There was much to discuss about the school policies and offerings, naturally. Noah 
    walked the group though the material and then opened the circle up to Q&A.

    As a break, next up was a craft project supplied by paper and colored pencils from 
    Kim’s pack.
    
    Not that she’d known it was the designated Craft Pack when she’d grabbed it from the 
    pile back at the trailhead; she’d just noticed it was lighter than the others, and she 
    hadn’t complained.

    And with that, there was a bit of leisure time left over to stretch and take a bathroom 
    break before they were due to report to the group picnic area for dinner with the other 
    teams.
    """

    # —

    # [bg darkens]
    show bg campsite_evening with dissolve

    nvl clear

    nvl_narrator """
    Apparently a number of supplemental volunteers had convened to host a barbecue.

    Feeding the entire freshman class was no easy feat; Kim had all the more respect for 
    the volunteer body for putting so much into the event.

    In the chaos of the dinner throng, she tried to skim the crowd for any other familiar 
    faces, such as Luziim or Emma, to little avail.

    Oh well, it was easier to stick with Team Cobalt, which wasn’t lacking in interesting 
    company, either.
    """

    # [bg darkens again]
    # TODO: more art??
    show bg campsite_night with dissolve

    nvl_narrator "The gathering ended with a presentation, or perhaps it was better to call it a welcome."
    
    nvl_narrator "A couple of the volunteers voiced platitudes about the formative years to come, as a 
    school and as a community."

    nvl_narrator "The speeches were, if a bit cliché, at least positive. The atmosphere of the event was one of optimism 
    and excitement. "

    nvl_narrator "Still, once it was over, Kim found herself much more eagerly walking back to Team 
    Cobalt’s campsite with her new friends."

    nvl_narrator "They had the rest of the night to enjoy each other’s company, and there was much yet to 
    share."

    nvl hide 
    with Dissolve(0.2)

    # —

    # [Nakoa and Karalún appear.]
    show nakoa happy at leftish_2_l
    show kara neutral at rightish_2_l
    with dissolve

    ki "Okay, step 3, fold these edges in here."

    "For starters, they’d found spare paper in the Craft Pack, and Kim had foolishly made an 
    idle comment about it."

    "Now both Nakoa and Karalún were her rapt audience as she attempted to demonstrate what 
    “origami” was."

    ka @ neutral talk "Like this?"

    "The Jorgoan curled the edges of the paper timidly inwards, looking up at Kim for 
    guidance."

    "Out of the corner of Kim’s eye, Nakoa was already meticulously pressing the crease with 
    an engineer’s precision. She focused on Karalún’s question."

    ki "Yes, like that! Line it up with the middle, then press the crease. You’re just 
    marking the paper for the next step."

    "Karalún patted the crease down one finger-press at a time."
    
    "She’d done that for the prior folds too; this time Kim noticed the way the amphibian 
    grimaced in frustration when the paper stuck to her fingers."

    ki "Do you need help with the paper? I can press the folds."

    ka worried @ worried talk "No, it’s fine, it’s fine! I’d like to do it."

    "She focused on the paper again—did her tendrils just flick?—and finished the crease 
    with a determined motion."

    ki "Yeah, there you go!"

    nk @ happy_talk "Alright, step 4?"

    ki "Step 4, this is the fun part. Use the creases you just made to kind of… {i}hinge{/i} the 
    paper upwards, like this."

    ki "You kind of flatten this bit here and invert the creases on the top half."

    show nakoa angry with dissolve_f

    "Both of her pupils looked on in puzzlement for a moment, before the lightbulbs clicked 
    on in their heads."

    nk happy @ happy_talk "Oh! Oh I get it, that’s super cool."

    ka @ worried talk "I think I… like this…?"

    "The Jorgoan swung the paper upwards—"

    show kara angry with dissolve_f

    "—then jolted upwards with a hiss."

    # Kim and Nakoa flinched. [do this w/ sprites, not a separate dialogue line]

    show nakoa worried at leftish_3_l with MoveTransition(0.2)

    "The Jorgoan’s tendrils writhed. Her eyes were narrowed, her lips pulled back to expose 
    her mouth full of hook-teeth…"

    ka worried @ worried talk "Ah, do we have a… medic… what do you call it? Medicine kit?"

    ki "A… first-aid kit? I think… Noah, who has the first aid kit?"

    "She called towards the other group across the campsite, internally kicking herself. Of 
    course Karalún’s grimace wasn’t a threat display; it was simple distress."

    # [Noah slides in.]

    show kara worried at center_1_l with ease
    show noah at rightish_3 with fadeinright

    no "What’s up? Someone get a cut?"

    no "Oooh, paper cut. Those little buggers’ll get ya. Hold on."

    "He flipped the first aid kit open on the picnic table and rifled through it for a few 
    seconds before selecting a small pouch from within."

    "Karalún dutifully held her hand steady for Noah to tend to with a dab of ointment."

    ka @ worried talk "Thank you."

    no "Yeah, no problem. Shouldn’t be too serious, but let me know if it still stings 
    later, and I’ll give you another dab."

    ki "Oh, do we not have a sealant?"

    no "Supposedly you can’t seal Jorgoan skin, it messes with the mucous layer. Is that 
    right?"

    ka neutral @ neutral talk "Ah, I’m not very knowing of the topic, but I think so."

    "Noah nodded."

    no "Knowledgeable, by the way. Knowledgeable of the topic."

    ka worried @ worried talk "Ah! Thank you, I’m sorry."

    no "No need to be sorry! You’re still learning. We’re all still constantly learning, 
    really."

    nk happy @ happy_talk "Words of wisdom, my man."

    "Nakoa closed up the first aid kit and slid onto the bench next to Kim."

    show noah at rightish_3_l with ease

    no "So how’re we all doing on this side of the camp? Any questions about the 
    presentation earlier, anything about the school we might’ve missed?"

    ki "Hmm, nothing that jumps out at me."

    show nakoa angry with dissolve_f
    "Nakoa cleared his throat."

    ki "Oh, no, wait! That’s right!"

    show nakoa happy 
    show kara neutral
    with dissolve_f

    ki "Can you tell us about the choral music offerings at the school?"

    no "Oooh, we’ve got choir kids in this group, huh? Now that’s an excellent question."

    no "NDU Kimmings has a pretty robust music program. The three main tracks of band, 
    orchestra, and choir all have multiple groups each, to cater to all skill levels."

    no "I can’t give you a hundred-percent accurate list of them all off the top of my head, but I 
    promise they’ve got you covered."

    no "And there’re student groups too, which you should see at the Club Fair next 
    weekend."

    ki "Oh, that does sound useful."

    nk @ happy_talk "Hey, Karalún, maybe there’ll be a club for you too! Brush up on your 
    martial arts."

    ka worried talk "Oh, I… yes, you’re right. I hadn’t thought of it."

    ka neutral talk "I am not… sure that I want to resume martial arts? It’s been a long time."

    ka excited "Actually, I am interested in the same thing as Kim. I would like to try music."

    no "Great! What kind? What do you play?"

    # [sprite: Karalún raises her hand to her chest and makes a fist.]

    ka worried @ worried talk "Uh, nothing. I do not… play anything. I will come to the group as a beginner."

    no "Hmm, an absolute beginner? That might be a bit of a tall order. I think all the 
    groups assume you can read music and know your way around your instrument."

    no "In that case, I’d suggest picking an instrument you like and seeing to the music 
    department for private lessons."

    no "Unless…"

    "Noah withdrew for a moment, stroking his chin in thought."

    no "Let me talk to my friend first. He’s a music major, might be interested in giving 
    you some rudimentary knowledge first if you don’t have any."

    ka happy @ grin "Oh! I, yes, I would like that. I am interested."

    no "Brilliant. Mind if we swap contacts, then? I’ll talk to him over the weekend, then 
    I can let you know how it goes."

    ka @ grin "Sounds great!"

    no "The same goes for all of you, by the way. I’m happy to give you my info, if 
    you ever want to chat after this orientation’s over."

    no "Not that I’ll be your leader anymore by that point. The RAs will have that handled. 
    But it can’t hurt to stay acquaintances."

    nk @ happy_talk "Sure, why not? You seem alright."

    ki "Yeah, good idea!"

    "They all took a minute to boot up their tabs, then tap wrists around the circle a pair 
    at a time to exchange contact data." 

    no "Well, I should go check on the others again. See how Allira’s campfire project is 
    going."

    ki "Ohh, is that what they’re doing over there? Trying to start the campfire?"

    nk smug "Aw, come now, have you no faith in the Lord of Candles to light a fire? For shame."

    # "That earned a collective chortle from the group. Noah stood and sidled off."
    "That earned a collective chortle from the group.{nw=0.2}"

    show nakoa happy
    show kara neutral
    with dissolve_f

    "That earned a collective chortle from the group.{fast} Noah stood and sidled off."
    
    show noah at rightish_3 with ease

    no "I’d like to do s’mores if we can get the fire big enough, but if you’re tired, feel 
    free to turn in whenever. You know where the bathrooms are."

    hide noah with fadeoutright

    show nakoa happy at leftish_2_l
    show kara neutral at rightish_2_l
    with ease

    "Kim glanced towards the communal restroom building a few campsites down the path. Then 
    she looked back to the other two."

    ki "Do we get changed into our pajamas in the bathrooms too? I dunno if there’s space, 
    we’re the entire freshman class here."

    nk friendly "We could use the tent. Take turns."

    nk happy @ happy_talk "I’m not going to bed yet, I’m waiting up for s’mores."

    ki "Ha ha, that’s fair."

    ka @ talk "Um, sorry, what is ‘s’mores’?"

    ki "Oooh, you’ll love s’mores. They’re a Terran campfire tradition. You roast 
    marshmallows by the fire, then turn them into little dessert sandwiches."

    ka @ talk "Roast… what?"

    nk @ happy_talk "It’s a gooey dessert, that’s all you need to know. We’ll explain once we get there, 
    it’ll make more sense."

    ka worried @ talk "R-right, I… I’m sorry."

    ki "Hey, no need to be sorry. It’s okay not to know things."

    ka @ talk "I know… I know. I’m sor- I mean…"

    "The telltale symptoms of a guilt spiral were a little too familiar to Kim. She racked 
    her brains for a distraction."

    ki "Hey, you said you wanted to get into music from the ground up? That’s neat! What 
    drove you to consider starting this late in life?"

    show kara neutral with dissolve_f

    ki "Er, sorry, that came out weird."

    ki "Nothing wrong with that, it’s just uncommon. Most kids in the Association start 
    doing music during primary school."

    nk @ happy_talk "Though whether they stick with it is another question."

    ka neutral talk "Oh, no, I did some music as a child. We sang anthems."

    ka pensive talk "But I never learned how the music worked, or how to… well, read the code on a page 
    and turn it into music in your head. I just copied everyone else."

    ka neutral talk "And now, here, in the Association, the music is so much {i}more{/i}. It all sounds so 
    different, and it all sounds different from each other. In the music apps on my tab, I 
    see {i}millions{/i} of tracks. It stuns me."

    ka angry "I want to know more. I want to know everything the Empire kept from me."

    "There was intensity in her eyes, an eager tremor in her tendrils."

    ki "I…"

    "She didn’t know what to say to that."

    ki "I can’t imagine what it must’ve been like. To live somewhere so different, and end 
    up here."

    ka neutral @ talk "Well, I’m still getting used to it. But I really like it here. I wouldn’t change 
    anything."

    ki "Heh. Well, we’re glad you’re enjoying it!"

    nk worried @ worried_talk "What did you do to get kicked out? If that’s not a rude question, I mean."

    "Kim flinched—that {i}was{/i} a rude question!—but Karalún was already answering."

    ka @ talk "Treason."

    nk @ worried_talk "...What kind of treason?"

    ki "Nakoa!"

    nk stern "What? She said it was fine."

    # [sprite: Karalún raises a fist to her chest.]

    ka @ talk "Breaking and entering, and petty theft."

    nk worried @ worried_talk "...Like, burglary? Broke into someone’s house? Not sure why that’s treason, unless 
    it was someone important."

    ka neutral talk "It was a government building. An archive."

    ka pensive @ talk "They did not want us to reveal what we found."

    ki "I… wow."

    ki "Alright, I admit, now I {i}am{/i} curious, but I feel like asking any more questions will 
    put me on a watch list."

    nk stern "Coward."

    ki "Come on! The Empire’s…"

    show nakoa worried
    show kara neutral
    with dissolve_f

    "She nearly said “scary,” but bit her tongue."

    ki "...You know, potentially threatening!"

    ka @ talk "I am no threat to you, Kim. You may ask me whatever you like."

    ki "Okay okay okay. So…"

    ki "What did you find that was so important? If it’s safe for you to reveal, I mean."

    nk @ worried_talk "...Not weapons codes, right?"

    ka neutral talk "No, nothing like that. We found… I believe you might call it a ‘museum.’"

    ka pensive talk "Relics of the time before the Empire. Tools, textiles, inscriptions in lost 
    languages. Proof that we used to be {i}more{/i}. We used to be many."

    ka neutral @ talk "I took as many photos as I could. And I paid the price. They caught me."

    nk @ worried_talk "And exiled you for… finding history they wanted to keep a secret?"

    nk @ stern "Why would they keep all that stuff if they didn’t want the populace finding out? 
    You’d think they’d have, I dunno, destroyed it or something."

    ka neutral talk "I don’t know. Maybe the government wants to preserve knowledge for itself that the 
    people can’t have. Or maybe it wants trophies of the past peoples that it dominated."

    ka pensive @ talk "I wish more than anything that I could go back in time. Finish what I started."

    "Karalún’s face and voice pinched. She looked away with a sniffle."

    ki "Hey, it’s okay. You were very brave. That’s what matters, yeah?"

    show kara worried with dissolve_f

    ki "Could you go back now? I know the Empire splintered a few years ago, they lost a  
    couple of colonies to the Association, right? If the border has weakened—"

    ka neutral talk "No. I am unwanted. {i}Avikran{/i}. An exile."

    ka worried talk "My family doesn’t even…"

    show kara pensive with dissolve_f

    "She choked on the words. She dipped her head, then shook with a quiet sob."

    ki "Hey, hey, it’s okay. Uh, it’s good that you’re here and alive! Right?"

    "What was she supposed to say to comfort someone in so odd a circumstance?"

    "Nakoa had started patting Karalún’s shoulder awkwardly."

    nk happy @ happy_talk "Well, hey, no better time to find a new family than here, right? We’re with ya."

    ka worried "..."

    ka happy "Thank you."

    "They were quiet for a moment, nodding and smiling weakly in agreement."

    ka neutral @ talk "So, um, what was that about a dessert?"

    ki "Oooh, right! Let’s see if they’ve started the s’mores yet."

    nk @ happy_talk "Right behind ya."

label chapter_2_b:

    # POV 2: Noah
    # [background: exterior of a dorm building]

    scene bg blake_noah_exterior with fade

    "It was midday Sunday." 

    "The freshmen and their chaperones had all been shuttled back to campus, and as Noah 
    walked back to his room, he felt a lingering, bittersweet glow."

    "They were good kids. Not at all unlike himself last year, waltzing onto this campus, 
    uncertain but enthusiastic nonetheless."

    "It was gratifying to be a part of giving them a welcome, to share in their 
    vulnerability from a place of experience."

    "Still, now that he’d been freed of the role of orientation group leader, he could let 
    out a sigh of relief. Holding himself with more authority than he felt he deserved over 
    his peers was taxing."

    # [background: interior of dorm hallway]

    show bg blake_noah_hall with dissolve 

    "Now all he had to do was relax until classes began tomorrow morning."

    "Much to his surprise, his dorm room door was wide open."

    # [background: room of Blake/Noah. Noah’s half of the room is furnished; Blake’s half is 
    # half-done with boxes visible.]

    show bg blake_noah_dorm with dissolve

    "A half-empty packing box propped the door open, and a certain two friends seemed to 
    have their hands full taping up an oversized poster over the bed on the far wall."

    # [Sprites: Blake and Tansei appear. Blake is beaming.]

    show tansei neutral at leftish_2_h
    show blake at rightish_2_h
    with dissolve

    # "After a moment, they noticed their new companion."

    b "Hey hey hey, there’s the roomie!"

    no "That’s my title now, huh?"

    show blake at rightish_2 with ease

    "But Blake was already bouncing down to ground level, arms already wide for a trademark 
    embrace."

    "A bit embarrassing, but Noah couldn’t not accept with a smile."

    show tansei neutral at leftish_2 with ease

    no "Well, someone’s excited to start the new term."

    b "Why you gotta make that sound like it’s weird? It’s good to be back."

    no "Yeah, yeah, I agree."

    no "Hey, Tani."

    t happy @ happy_talk "Hey, Noah."

    no "This bloke isn’t running you ragged yet, is he?"

    t neutral_talk "I freely volunteered to help Blake move in and get settled, but thank you for 
    caring."

    t happy @ happy_talk "Don’t worry, he owes me the same now. We were just about to head over to Cressa Hall 
    to do my room."

    no "Tit for tat, mm? Guess that keeps things fair."

    t gesture @ gesture_talk "Oh, but we don’t want to be a bore now that you’re here. Want to take a break? Grab 
    a drink or a snack?"

    no "Oh, no, don’t worry about me. I’ll come along and help, I’ve got nothing better to 
    do."

    b "Careful talkin’ like that, you’re letting me off easy."

    no "Fine, fine. I’ll get myself a cuppa and watch you two work, if that’s how you like 
    it."

    show tansei laughing with dissolve_f

    "Tansei cackled."

    t happy @ happy_talk "Alright, compromise. We’ll {i}all{/i} take a caffeine break, and then {i}all{/i} help with my 
    room. {i}Prekessai?{/i}"

    "Noah and Blake nodded in agreement."

    no "{i}Prekessa.{/i}" (multiple=2)

    b "{i}Prekessa.{/i}" (multiple=2)

    "It was decided. After a cursory checking of everyone’s pockets for their essentials, 
    the group left their room behind and was on their way down the hall."

    # [background returns to dorm hallway.]

    show bg blake_noah_hall with dissolve

    b "So how were the freshmen?"

    no "Alright. Not much to report, they all seemed like good kids."

    b "You say that like they’re not basically the same age as you."

    no "Okay, okay, {i}good young adults,{/i} better?"

    show tansei laughing with dissolve_f

    "Tansei snorted in laughter."

    t happy @ happy_talk "Speak for yourself, Blake. My brother’s in the freshman class and he is 
    a hundred and ten percent still a kid to me."

    no "There, see? It’s not a weird way to think."

    b "Kids, shmids, whatever."

    # [background changes to campus walkway]

    no "Anyway, yeah, it was fun to lead, be a welcoming face. Hopefully I made bonds with 
    at least a couple of ’em, but we’ll see."

    no "My group kind of split in half early on in the trip. Sort of a letdown they weren’t 
    commingling more, but I guess it’s only natural for people to clique up."

    b "Aw, yeah, that’s a bummer, but I can see it."

    t neutral @ neutral_talk "Anyone leave an impression on you? Any friends you think might last?"

    no "Well, I don’t know about friends, but the most interesting one was definitely the 
    Jorgoan. Wasn’t expecting to get one in my group."

    "There was a pause."

    b "Something wrong with there being a Jorgoan in your group?"

    no "No, no, nothing like that! It just struck me, is all. What with them being a 
    minority, and with such a…{nw=0.5}"
    extend " y’know, unique background."

    b "Alright, that’s fair. Avikran, Imperial, or secessionist?"

    no "Oh, got to have been Avikran. Her Standard was too good for her to be new here."

    b "Oof. Gotta be a hell of a backstory."

    no "Wasn’t my place to pry, but yeah, there’s a tale there."

    no "She did spook me at one point. Those teeth’ll—"

    "He stopped himself."

    no "Those teeth were impressive."

    t @ neutral_talk "You can say ‘Those teeth’ll scare you,’ it’s okay. We’re all thinking it."

    b "Tansei!"

    t stern @ stern_talk "What? It’s true."

    no "No, no, I get it. There’s so much more to Jorgoans than scary teeth, and it was my 
    job to look past that and be welcoming. Like, there’s tragedy there."

    show tansei neutral with dissolve_f

    no "She’s as eager to be here as anyone. Maybe even more than most."

    no "Ooh, right. And she said she has no music background, but wants to start. To learn 
    entirely from scratch."

    "Blake’s eyes lit up immediately. {i}Bingo.{/i}"

    b "{cps=*1.5}Like, from scratch, from scratch? Like I should get her on a recorder book for third 
    graders?{/cps}"

    b "{cps=*1.5}No, but you don’t want to bore her with plain melodies, either. Adults are too smart 
    for that.{/cps}"

    b "{cps=*1.5}I’ll need to weave in some high-level stuff to draw her in, tickle her fancy. A 
    conceptual intro, a tour of the universal constant of music.{/cps}"

    "And he was cooking already."

    no "I didn’t even ask you to teach her yet, dude."

    b "Oh come on, you were leaning into that. Don’t deny it."

    b "Anyway! Do we have a date?"

    no "What? No, I told her I’d ask you first."

    b "Then let’s set a date! This Saturday, after the first week of classes."

    t @ neutral_talk "Isn’t that the day of the Club Fair?"

    b "Damn, you’re right."

    b "Day after! Next Sunday."

    no "Well, I’ll pitch that to her. We’ll see if she’s still interested."

    b "Excellent."

    "He took a deep inhale at the concluded thought."

    b "Good to be back, ain’t it?"

    no "Yeah, it’s good to be back."

    # return
    jump chapter_3